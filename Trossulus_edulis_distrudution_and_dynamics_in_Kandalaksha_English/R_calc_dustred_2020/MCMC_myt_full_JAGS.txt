
model{
    #1A. Priors beta and sigma
    for (i in 1:K) { beta[i] ~ dnorm(0, 0.0001)}

    #1B. Priors random effects and sigma
    for (i in 1:Nre) { a[i] ~ dnorm(0, tau_myt)}
    tau_myt <- 1 / (sigma_myt * sigma_myt)
    num           ~ dnorm(0, 0.0016)             #<----half-Cauchy(25)
    denom         ~ dnorm(0, 1)                  #<----half-Cauchy(25)
    sigma_myt   <- abs(num / denom)              #<----half-Cauchy(25)

    #2. Likelihood
    for (i in 1:N) {
      Y[i]         ~ dbin(Pi[i], Trials[i])  
      logit(Pi[i]) <- eta[i] 
      eta[i]      <- inprod(beta[], X[i,]) + a[Site[i]] 
  
      #Saving computer time
      ##3. Discrepancy measures 
      Exp[i] <- Pi[i] * Trials[i] 
      Var[i] <- Pi[i] * Trials[i] * (1 - Pi[i])
      E[i]   <- (Y[i]  - Exp[i]) / sqrt(Var[i])    
     
      YNew[i] ~  dbin(Pi[i], Trials[i])            #Simulated data with mean/variance taken from the fitted model
      ENew[i] <- (YNew[i] - Exp[i]) / sqrt(Var[i]) #Normalized residual for predicted data
      D[i]    <- pow(E[i], 2)                      
      DNew[i] <- pow(ENew[i], 2)   
     }          
     Fit         <- sum(D[1:N])                     #Sum of squared residuals  
     FitNew      <- sum(DNew[1:N])                  #Sum of squared predicted residuals
}

